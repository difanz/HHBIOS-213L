;	READ1.ASM (2.13L)
;	1993.11.10
;	1995.1.24, 3.1, 4.8
;	1999.11.2

SEG_A		SEGMENT
		ASSUME	CS:SEG_A, DS:SEG_A

		ORG	100H
START:		JMP	BEGIN

;子程序:读下一分区BOOT
READBOOT	PROC	NEAR
		MOV	SI,1BEH 		;分区表首
L_RB1:
		CMP	BYTE PTR [BX+SI+4],5	;判扩展分区
		JZ	L_RB2
		ADD	SI,10H			;转下一分区
		JMP	SHORT L_RB1
L_RB2:
		MOV	CX,[BX+SI+2]
		MOV	DX,[BX+SI]
		OR	DL,80H
		MOV	AX,201H
		INT	13H			;读下一分区BOOT
		RET
READBOOT	ENDP

;子程序:建立扇区链表 AL=字库名(J-简体,F-繁体),BP=存放区
S_SET		PROC	NEAR
		MOV	AH,AL
		CMP	AL,'F'                  ;1.24
		JZ	L_SET0
		MOV	AX,20H
L_SET0:
		MOV	D_FILE[6],AL
		MOV	D_HZK16[5],AH

		MOV	DX,OFFSET D_DTA 	;DTA地址
		MOV	DI,DX
		MOV	AH,1AH
		INT	21H
		MOV	DX,OFFSET D_FILE	;文件名
		MOV	AH,11H			;查找目录项
		INT	21H
		OR	AL,AL
		JZ	L_SETA
		STC
		RET
L_SETA:
		MOV	AX,[DI+1BH]		;首簇号
		PUSH	AX
		SUB	AX,2
		MUL	WORD PTR DS:[0F0H]	;每簇扇区数
		ADD	AX,DS:[0F4H]		;文件区首扇区号
		ADC	DX,0
		MOV	DS:[BP],AX		;存连续块首扇区号
		MOV	DS:[BP+2],DX		;  >64K
		POP	AX
		MOV	WORD PTR DS:[0F6H],0	;簇计数清0
L_SET1:
		PUSH	AX
		INC	WORD PTR DS:[0F6H]	;簇计数+1
		MOV	SI,AX
		CMP	BYTE PTR DS:[0FFH],1	;判12位FAT?
		JZ	L_SET3
		MOV	ES,DS:[0F8H]		;FAT表段
		ADD	SI,AX
		JNB	L_SET2
		MOV	ES,DS:[0FAH]		; >64K
L_SET2:
		MOV	AX,ES:[SI]		;下一簇号
		CMP	AX,0FFF8H		;判结束?
		JMP	SHORT L_SET6
L_SET3:
		SHR	AX,1
		PUSHF
		ADD	SI,AX
		MOV	AX,ES:[SI]		;下一簇号
		POPF
		JNC	L_SET4
		MOV	CL,4
		SHR	AX,CL
		JMP	SHORT L_SET5
L_SET4:
		AND	AX,0FFFH
L_SET5:
		CMP	AX,0FF8H		;判结束?
L_SET6:
		POP	SI
		JB	L_SET7
		MOV	WORD PTR DS:[BP+4],0FFFFH	;结束标志
		ADD	BP,6
		CLC
		RET
L_SET7:
		INC	SI			;簇号+1
		CMP	AX,SI			;判连续?
		JZ	L_SET1

		PUSH	AX
		MOV	AX,DS:[0F0H]		;每簇扇区数
		MUL	WORD PTR DS:[0F6H]	;×簇计数
		MOV	DS:[BP+4],AX		;扇区数
		MOV	WORD PTR DS:[0F6H],0	;簇计数清0

		MOV	AX,DS:[0F0H]		;每簇扇区数
		POP	SI
		PUSH	SI
		SUB	SI,2			;簇号-2
		MUL	SI			;得扇区号
		ADD	AX,DS:[0F4H]		;+文件区首扇区号
		ADC	DX,0
		ADD	BP,6
		MOV	DS:[BP],AX		;存连续块首扇区号
		MOV	DS:[BP+2],DX		; >64K
		POP	AX
		JMP	SHORT L_SET1
S_SET		ENDP

		ORG	230H
D_HZKDS 	DW	0			;内存字库段
D_020A		DW	0			;扇区字节数
D_020C		DW	0			;每道扇区数
D_020E		DW	0			;磁头数
D_0225		DB	2			;当前盘
D_0226		DW	0			;分区首扇区号
D_0228		DW	0			;      磁柱号

	INCLUDE EXIT.INC

S_EX		PROC	NEAR
		RET
S_EX		ENDP

INT_7F		PROC	FAR
		STI
		CLD
		PUSH	AX
		PUSH	BX
		PUSH	DS
		PUSH	CS
		POP	DS
		AND	DX,7F7FH

		MOV	BX,D_JT 		;1.24
		OR	AH,AH			;=0繁体
		JNZ	L_1			;非0简体
		MOV	BX,D_FT
		JMP	SHORT L_30
L_1:
		CMP	DH,24H			;1~3区?
		JB	L_20
		CMP	DH,29H			;9区?
		JNE	L_10
		MOV	DH,24H
		JMP	SHORT L_20
L_10:
		CMP	DH,30H			;<16区
		JB	L_30
		CMP	DH,57H			;>55区
		JA	L_30
		SUB	DH,0BH
L_20:
		SUB	DX,2121H
		MOV	AL,94
		MUL	DH
		XOR	DH,DH
		ADD	AX,DX
		SHL	AX,1
		ADD	AX,D_HZKDS
		MOV	DX,AX
		POP	DS
		POP	BX
		POP	AX
		IRET
INT_7F		ENDP

L_30:
		SUB	DX,2121H
		MOV	AL,94
		MUL	DH
		XOR	DH,DH
		ADD	AX,DX

		PUSH	CX
		PUSH	DI
		PUSH	SI
		PUSH	ES
		PUSH	CS
		POP	ES
		XOR	DX,DX
		MOV	CX,10H
		DIV	CX
		PUSH	DX
L_40:
		CMP	AX,[BX+4]
		JB	L_50
		SUB	AX,[BX+4]
		ADD	BX,6
		JMP	SHORT L_40
L_50:
		MOV	DX,DS:[BX+2]
		ADD	AX,DS:[BX]
		ADC	DX,0
		DIV	D_020C
		ADD	DX,D_0226		;加分区首扇区号
		CMP	DX,D_020C
		JBE	L_60
		MOV	DX,1
		INC	AX
L_60:
		MOV	CL,DL			;→扇区号

		XOR	DX,DX
		DIV	D_020E
		MOV	DH,DL
		ADD	AX,D_0228		;加分区首磁柱号
		MOV	CH,AL
		MOV	AL,40H
		MUL	AH
		ADD	CL,AL
		MOV	DL,80H
		MOV	BX,0
		MOV	AX,201H
		INT	13H

		POP	AX
		OR	AX,AX
		JZ	L_70
		MOV	CX,20H
		MUL	CX
		ADD	AX,BX
		MOV	SI,AX
		XOR	DI,DI
		REP	MOVSB
L_70:
		POP	ES
		POP	SI
		POP	DI
		POP	CX
L_80:
		PUSH	CS
		POP	DX
		POP	DS
		POP	BX
		POP	AX
		IRET

D_JT		DW	0
D_FT		DW	0
D_BUF		DB	512 DUP (0)

;程序开始执行****************************************************************
;格式:READ1 [J][F]

BEGIN:
		MOV	AX,4A06H
		MOV	SI,3			;取驻留状态
		INT	2FH
		CMP	BX,4A06H		;判是否已驻留?
		JNZ	L_S10
		MOV	DX,OFFSET D_ALREADY
L_ERR:
		MOV	AH,9
		INT	21H
		MOV	AX,4C01H
		INT	21H
L_S10:
		MOV	AX,CS
		CMP	AX,0A000H		;LH?
		JB	L_S12
		MOV	DX,OFFSET D_UMB
		JMP	SHORT L_ERR
L_S12:
		MOV	SP,0F0H
		MOV	AX,DS:[5DH]
		AND	AX,5F5FH
		MOV	DS:[101H],AX

		MOV	AH,19H			;取当前盘
		INT	21H
		CMP	AL,2			;判硬盘?
		JNB	L_S20
		MOV	DX,OFFSET D_FDISK
		JMP	SHORT L_ERR
L_S20:
		MOV	D_0225,AL		;当前盘号
		INC	AL
		MOV	D_FILE,AL

		MOV	AX,201H
		MOV	BX,OFFSET D_DTA
		MOV	CX,1
		MOV	DX,80H
		INT	13H

		DEC	D_0225
L_S22:
		DEC	D_0225
		JZ	L_S24
		CALL	READBOOT		;读下一分区BOOT
		JMP	SHORT L_S22
L_S24:
		PUSH	CX
		MOV	DL,CH			;磁柱号低8位
		XOR	CH,CH
		SHL	CX,1			;磁柱号高2位移CH
		SHL	CX,1
		MOV	DH,CH
		POP	CX
		AND	CX,3FH
		MOV	D_0226,CX		;分区首扇区号
		MOV	D_0228,DX		;      磁柱号

		MOV	SI,1BEH
L_S30:
		MOV	AL,DS:[BX+SI+4]
		CMP	AL,1
		JZ	L_S40
		CMP	AL,4
		JZ	L_S40
		CMP	AL,6
		JZ	L_S40
		ADD	SI,10H
		JMP	SHORT L_S30
L_S40:
		MOV	DS:[0FFH],AL
		MOV	CX,[BX+SI+2]
		MOV	DX,[BX+SI]
		OR	DL,80H
		MOV	AX,201H
		INT	13H

		PUSH	DX
		MOV	AX,[BX+0BH]		;扇区字节数
		MOV	D_020A,AX
		MOV	AX,[BX+18H]		;每道扇区数
		MOV	D_020C,AX
		MOV	AX,[BX+1AH]		;磁头数
		MOV	D_020E,AX
		MOV	AL,[BX+0DH]		;每簇扇区数
		XOR	AH,AH
		MOV	DS:[0F0H],AX
		MOV	AX,[BX+16H]		;FAT扇区数
		MUL	BYTE PTR [BX+10H]
		ADD	AX,[BX+0EH]		;+保留扇区数
		ADD	AX,[BX+1CH]		;+隐含扇区数
		MOV	DS:[0F4H],AX
		MOV	AX,20H
		MUL	WORD PTR [BX+11H]	;根目录项数
		DIV	D_020A
		ADD	DS:[0F4H],AX
		POP	DX

		MOV	AX,CS
		ADD	AX,1000H
		MOV	DS:[0F8H],AX		;FAT读入第一段
		MOV	ES,AX
		ADD	AX,1000H
		MOV	DS:[0FAH],AX		;FAT读入第二段

		MOV	AX,[BX+16H]		;每FAT表扇区数	4.8
		PUSH	AX
		CMP	AX,80H			;判FAT超64K?	4.8
		JBE	L_S50
		MOV	AL,80H
L_S50:
		XOR	BX,BX
		INC	CX
		MOV	AH,2
		INT	13H
		ADD	CL,80H
		MOV	AL,CL
		XOR	AH,AH
		DIV	BYTE PTR D_020C 	;每道扇区数
		MOV	CL,AH
		ADD	AL,DH
		XOR	AH,AH
		DIV	BYTE PTR D_020E 	;磁头数
		MOV	DH,AH
		ADD	CH,AL
		POP	AX

		CMP	AX,80H			;判FAT超64K?	4.8
		JBE	L_S60
		SUB	AL,80H
		MOV	ES,DS:[0FAH]		;FAT读入第二段
		XOR	BX,BX
		MOV	AH,2
		INT	13H
L_S60:
		MOV	ES,DS:[0F8H]		;指向FAT首
		MOV	BP,OFFSET D_BUF

		MOV	AL,DS:[101H]		;[5DH]	1.24
		CMP	AL,'J'
		JZ	L_S62
		CMP	AL,'F'
		JZ	L_S62
		MOV	AL,'J'
		MOV	DS:[101H],AL
L_S62:
		MOV	D_JT,BP
		MOV	D_FT,BP
		CALL	S_SET			;建FAT索引表
		JNB	L_S64
		MOV	DX,OFFSET D_NOFILE
		JMP	L_ERR
L_S64:
		MOV	AL,DS:[102H]		;第二字库
		CMP	AL,'J'
		JNZ	L_S66
		MOV	D_JT,BP
		JMP	SHORT L_S67
L_S66:
		CMP	AL,'F'
		JNZ	L_S68
		MOV	D_FT,BP
L_S67:
		CALL	S_SET			;建扇区链表
L_S68:
		MOV	AX,OFFSET D_DTA
		MOV	CL,4
		SHR	AX,CL
		INC	AX
		MOV	DX,CS
		ADD	AX,DX
		MOV	D_HZKDS,AX

		MOV	DX,OFFSET D_HZK16
		MOV	AX,3D00H		;打开文件
		INT	21H
		JNB	L_S70
		MOV	DX,OFFSET D_NOFILE
		JMP	L_ERR
L_S70:
		MOV	BX,AX
		MOV	DS,D_HZKDS
		XOR	DX,DX
		MOV	CX,2340H
		MOV	AH,3FH			;读1-3区
		INT	21H
		MOV	DX,3AC0H
		XOR	CX,CX
		MOV	AX,4201H		;定位
		INT	21H
		MOV	AX,DS
		ADD	AX,234H
		MOV	DS,AX
		XOR	DX,DX
		MOV	CX,0BC0H
		MOV	AH,3FH			;读9区
		INT	21H
		MOV	DX,4680H
		XOR	CX,CX
		MOV	AX,4201H		;定位
		INT	21H
		MOV	AX,DS
		ADD	AX,0BCH
		MOV	DS,AX
		XOR	DX,DX
		MOV	CX,0EB00H
		MOV	AH,3FH			;读一级字库
		INT	21H
		MOV	AX,DS
		ADD	AX,0EB0H
		MOV	DS,AX
		MOV	AH,3FH			;读一级字库
		INT	21H
		MOV	AH,3EH			;关闭文件
		INT	21H

		MOV	AX,DS
		ADD	AX,0EB0H
		MOV	DX,CS
		SUB	AX,DX
		ADD	AX,10H
		PUSH	AX
		PUSH	CS
		POP	DS

		MOV	DX,OFFSET INT_7F
		MOV	AX,257FH
		INT	21H

	INCLUDE EXIT1.INC			;1.24

		MOV	BYTE PTR DS:[100H],'1'
		POP	DX
		MOV	AX,3100H
		INT	21H

D_HZK16 	DB	'HZK16',0,0
D_FILE		DB	3,'HZK16      '
		DB	20 DUP (0)

D_ALREADY	DB	'READ1 IS ALREADY!',7,13,10,'$'
D_UMB		DB	'READ1 NOT INSTALL TO UMB!',7,13,10,'$'
D_FDISK 	DB	'READ1 NOT INSTALL TO FLOPYDISK!',7,13,10,'$'
D_NOFILE	DB	'NOT FOUND THE FILE!',7,13,10,'$'
D_DTA	EQU	$

SEG_A		ENDS
		END	START
