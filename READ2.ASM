;	FILE2.ASM (2.13L)
;	1993.11.11
;	1995.1.24

SEG_A		SEGMENT
		ASSUME	CS:SEG_A, DS:SEG_A

		ORG	100H
;格式:READ2 a
;其中: a 为 F 时读繁体字库,否则读简体

START:
		MOV	AX,4A06H
		MOV	SI,3			;取驻留状态
		INT	2FH
		CMP	BX,4A06H		;判是否已驻留?
		JNZ	L_10
		MOV	DX,OFFSET D_ALREADY
L_ERR:
		MOV	AH,9
		INT	21H
		MOV	AX,4C01H
		INT	21H
L_10:
		MOV	AX,CS
		CMP	AX,0A000H		;LH?
		JB	L_20
		MOV	DX,OFFSET D_UMB
		JMP	SHORT L_ERR
L_20:
		MOV	SP,100H

		MOV	AL,DS:[5DH]
		AND	AL,5FH
		CMP	AL,'F'
		JNZ	L_30
		MOV	D_FILE[5],AL
		JMP	SHORT L_40
L_30:
		MOV	AL,'J'
L_40:
		MOV	DS:[101H],AL

		MOV	DX,OFFSET D_FILE
		MOV	AX,3D00H		;打开文件
		INT	21H
		JNB	L_50
		MOV	DX,OFFSET D_NOFILE
		JMP	SHORT L_ERR
L_50:
		MOV	BX,AX
		MOV	AX,CS
		MOV	DX,OFFSET D_BUF
		MOV	CL,4
		SHR	DX,CL
		INC	DX
		ADD	AX,DX
		MOV	WORD PTR DS:D_HZKDS,AX
		MOV	DS,AX
L_60:
		XOR	DX,DX
		MOV	CX,8000H		;32K
		MOV	AH,3FH			;读文件
		INT	21H
		OR	AX,AX
		JZ	L_70
		MOV	AX,DS
		ADD	AX,800H
		MOV	DS,AX
		JMP	SHORT L_60
L_70:
		MOV	AH,3EH			;关闭文件
		INT	21H

		MOV	AX,DS
		MOV	BX,CS
		SUB	AX,BX
		PUSH	AX
		PUSH	CS
		POP	DS

		MOV	DX,OFFSET INT_7F
		MOV	AX,257FH
		INT	21H

	INCLUDE EXIT1.INC			;1.23

		MOV	BYTE PTR DS:[100H],'2'
		POP	DX
		MOV	AX,3100H
		INT	21H

	INCLUDE EXIT.INC

S_EX		PROC	NEAR
		RET
S_EX		ENDP

INT_7F		PROC	FAR
		PUSH	AX
		AND	DX,7F7FH
		SUB	DX,2121H
		MOV	AL,5EH
		MUL	DH
		XOR	DH,DH
		ADD	AX,DX
		SHL	AX,1
		ADD	AX,0
D_HZKDS EQU	$-2
		MOV	DX,AX
		POP	AX
		IRET
INT_7F		ENDP

D_BUF		EQU	$

D_FILE		DB	'HZK16',0,0
D_ALREADY	DB	7,13,10,'READ2 IS ALREADY!',13,10,'$'
D_NOFILE	DB	7,13,10,'NOT FOUND THE FILE!',13,10,'$'
D_UMB		DB	7,13,10,'READ2 NOT INSTALL TO UMB!',13,10,'$'

SEG_A		ENDS
		END	START
