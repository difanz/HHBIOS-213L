;	CCDISP.ASM (2.13L)
;	1995.1.24, 10.7, 12.17
;	1999.11.17

CSEG		SEGMENT
		ASSUME	CS:CSEG, DS:CSEG

		ORG	100H
START:
		XOR	AX,AX
		MOV	ES,AX
		MOV	AL,ES:[4F0H]		;显示模块号
		OR	AL,AL
		JZ	TESTV
		CMP	AL,5			;=7 GW, 56空
		JBE	L_10
		JMP	QUIT
L_10:
		DEC	AL
		MOV	AH,10
		MUL	AH
		ADD	AX,OFFSET D_FILE
		MOV	SI,AX
		MOV	DX,AX
		MOV	DI,82H
		PUSH	CS
		POP	ES
		CALL	S_MOVE
		MOV	WORD PTR DS:[80H],2020H

		MOV	AX,3D00H		;打开文件
		INT	21H
		JNB	L_20
		CALL	S_PATH			;查找本程序路径并形成全路径名
		MOV	DX,82H
		MOV	AX,3D00H
		INT	21H
		JNB	L_20
		MOV	DX,OFFSET NOFILE
		JMP	L_ERR
L_20:
		MOV	BX,AX
		PUSH	CS
		POP	ES
		MOV	SI,OFFSET L_A
		MOV	DI,0A0H
		MOV	CX,OFFSET TESTV-OFFSET L_A
		REP	MOVSB			;以下程序拷贝到CS:0A0H
		MOV	AX,0A0H
		JMP	AX
L_A:
		MOV	DX,100H
		MOV	CX,-1
		MOV	AH,3FH			;读显示模块
		INT	21H
		MOV	AH,3EH			;关闭文件
		INT	21H
		MOV	AX,100H
		JMP	AX			;执行
TESTV:
		MOV	AX,1A00H
		INT	10H
		CMP	AL,1AH			;判VGA卡?
		JNZ	TESTE
		MOV	AL,2
		JMP	L_10
TESTE:
		MOV	AX,1200H
		MOV	BL,10H
		INT	10H
		CMP	BL,10H			;判EGA卡?
		JZ	TESTC
		MOV	AL,1
		JMP	L_10
TESTC:
		MOV	DX,3D4H
		MOV	AL,0FH
		OUT	DX,AL
		INC	DX
		IN	AL,DX
		PUSH	AX
		XOR	AL,0FFH
		MOV	AH,AL
		OUT	DX,AL
		IN	AL,DX
		CMP	AH,AL
		POP	AX
		OUT	DX,AL
		JNZ	TESTH			;判CGA卡?
		MOV	AL,4
		JMP	L_10
TESTH:
		MOV	DX,3B4H
		MOV	AL,0FH
		OUT	DX,AL
		INC	DX
		IN	AL,DX
		PUSH	AX
		XOR	AL,0FFH
		MOV	AH,AL
		OUT	DX,AL
		IN	AL,DX
		CMP	AH,AL
		POP	AX
		OUT	DX,AL
		JNZ	TESTR			;判单显卡?
		MOV	AL,3
		JMP	L_10
TESTR:
		MOV	DX,OFFSET NO6845
L_ERR:
		MOV	AH,9
		INT	21H
QUIT:
		MOV	AX,4C01H
		INT	21H

;子程序:查找本程序路径
S_PATH		PROC	NEAR
		PUSH	DX
		MOV	DS,DS:[2CH]		;环境段
		XOR	SI,SI
L_P1:
		CMP	WORD PTR DS:[SI],0	;查找环境区尾?
		JZ	L_P2
		INC	SI
		JMP	SHORT L_P1
L_P2:
		ADD	SI,4
		MOV	DI,82H			;带路径源文件名区
		PUSH	CS
		POP	ES
		CALL	S_MOVE
		PUSH	CS
		POP	DS
L_P4:
		CMP	BYTE PTR [DI-1],'\'
		JZ	L_P5
		CMP	BYTE PTR [DI-1],':'
		JZ	L_P5
		DEC	DI
		JMP	SHORT L_P4
L_P5:
		POP	SI
S_MOVE:
		LODSB
		STOSB
		OR	AL,AL
		JNZ	S_MOVE
		RET
S_PATH		ENDP

D_FILE		DB	'EGA.COM',0,0,0
		DB	'VGA.COM',0,0,0
		DB	'HGA.COM',0,0,0
		DB	'CGA.COM',0             ;10.7
		DB	'CGA11.COM',0
NO6845		DB	'非标准显示卡!',7,13,10,'$'
NOFILE		DB	'Not found the file!',7,13,10,'$'

CSEG		ENDS
		END	START
