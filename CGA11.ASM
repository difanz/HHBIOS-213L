;	CGA11.ASM
;	1996.2.10, 10.10

E_HS	EQU	9				;工作区行数-1
E_HLINE EQU	9				;每行扫描线数
E_HBYTE EQU	9*80				;每行字节数
E_WLINE EQU	10*9				;文本区扫描线数
E_WBYTE EQU	10*9*80 			;文本区字节数

SEG_A		SEGMENT
		ASSUME	CS:SEG_A, DS:SEG_A

		ORG	100H
START:		JMP	BEGIN

D_ZFQ		EQU	110H			;字符区
D_SXQ		EQU	D_ZFQ+7D0H		;属性区
D_BZQ		EQU	D_SXQ+7D0H

		ORG	1880H
D_49		DB	6			;显示方式
D_4A		DB	80, 0			;每行字符数
D_50		DW	0			;光标位置
D_52		DB	0			;光标状态
D_54		DB	0			;光标开关
D_56		DB	0,0			;提示行光标位置
D_5E		DB	2			;光标大小
D_71		DW	0
D_78		DB	32 DUP (0)
D_A5		DB	0			;窗口顶行
D_A6		DB	E_HS			;    底行
D_A9		DB	0			;提示行汉字前字节
D_0069		DB	1			;=0繁体,非0简体
D_HZ9		DB	0			;汉字九区
D_5D16		DB	18			;16线或18线方式
D_INT10 	DD	0
D_INT16 	DW	0

D_AH		DW	OFFSET L_AH00		;AH=0
D_AH01		DW	OFFSET L_AH01		;AH=1
		DW	OFFSET L_AH02		;AH=2
		DW	OFFSET L_AH03		;AH=3
		DW	OFFSET L_INT10		;AH=4
		DW	OFFSET L_INT10		;AH=5
		DW	OFFSET L_AH06		;AH=6
		DW	OFFSET L_AH07		;AH=7
		DW	OFFSET L_AH08		;AH=8
		DW	OFFSET L_AH09		;AH=9
		DW	OFFSET L_AH09		;AH=0AH
		DW	OFFSET L_INT10		;AH=0BH
		DW	OFFSET L_INT10		;AH=0CH
		DW	OFFSET L_INT10		;AH=0DH
		DW	OFFSET L_AH0E		;AH=0EH
		DW	OFFSET L_INT10		;AH=0FH
		DW	OFFSET L_INT10		;AH=10H
		DW	OFFSET L_INT10		;AH=11H
		DW	OFFSET L_INT10		;AH=12H
		DW	OFFSET L_AH13		;AH=13H
		DW	OFFSET L_AH14		;AH=14H
		DW	OFFSET L_AH15		;AH=15H
		DW	OFFSET L_AH16		;AH=16H
		DW	OFFSET L_AH17		;AH=17H
		DW	OFFSET L_AH18		;AH=18H

INT_10		PROC	FAR
		STI
		CLD
		CMP	AH,-1			;判安装?		2.10
		JNZ	L_00
		MOV	AL,'c'                  ;CGA11
		CBW
		IRET
L_00:
		OR	AH,AH
		JZ	L_01
		CMP	AH,18H			;功能号>17H?
		JA	L_10
		CMP	CS:D_49,4		;判西文方式?
		JB	L_10
L_01:
		PUSH	DS			;保存现场
		PUSH	ES
		PUSH	BP
		PUSH	SI
		PUSH	DI
		PUSH	DX
		PUSH	CX
		PUSH	BX
		PUSH	AX

		PUSH	CS
		POP	DS
		XOR	DI,DI
		MOV	ES,DI

		XCHG	AL,AH
		MOV	DI,AX
		AND	DI,7FH
		SHL	DI,1
		XCHG	AL,AH
		JMP	D_AH[DI]
L_INT10:
		POP	AX			;恢复现场并执行原INT10
		POP	BX
		POP	CX
		POP	DX
		POP	DI
		POP	SI
		POP	BP
		POP	ES
		POP	DS
L_10:
		JMP	CS:D_INT10

;子程序:执行原INT10
S_INT10 	PROC	NEAR
		PUSHF
		CALL	CS:D_INT10
		RET
S_INT10 	ENDP

;AH=0 设置显示方式
L_AH00:
		MOV	D_49,AL
		MOV	ES,D_INT16		;键盘模块段

		CMP	AL,4			;判字符方式?
		JAE	L_0001
		MOV	ES:[101H],AL
		JMP	SHORT L_INT10
L_0001:
		CMP	AL,6
		JBE	L_0010
		MOV	AL,6
L_0010:
		MOV	ES:[100H],AL
		MOV	ES:[101H],AL
		CALL	S_INT10 		;设置显示方式

		MOV	AH,0BH
		MOV	BX,102H
		CALL	S_INT10 		;设置颜色:绿
L_0020:
		MOV	AL,80
		CMP	D_49,6
		JZ	L_0030
		MOV	AL,40
L_0030:
		MOV	D_4A,AL

		MOV	AX,-1
		MOV	D_54,AL
		XOR	AX,AX
		MOV	D_50,AX

		MOV	D_A5,AL
		MOV	D_A6,E_HS		;9

		PUSH	CS
		POP	ES
		MOV	CX,25*80
		MOV	DI,D_ZFQ
		REP	STOSB
		MOV	CX,25*80
		MOV	DI,D_BZQ
		REP	STOSB
		MOV	CX,25*80
		MOV	DI,D_SXQ
		MOV	AL,3
		REP	STOSB

		XOR	BP,BP
		CALL	S_XSGB			;显示光标
;返回
L_RET:
		POP	AX			;恢复现场并返回
L_RET8:
		POP	BX
		POP	CX
		POP	DX
L_RET3:
		POP	DI
		POP	SI
		POP	BP
L_RET1406:
		POP	ES
		POP	DS
		IRET
INT_10		ENDP

;AH=1 设置光标大小 CH=起始扫描线,CL=终止线
L_AH01:
		CLI
		OR	CX,CX
		JZ	L_0103
		MOV	BP,1
		CALL	S_XSGB			;关闭光标
		PUSH	CX
		AND	CX,1F1FH
		CMP	CL,CH
		JNB	L_0101
		MOV	CH,CL
L_0101:
		SUB	CL,CH
		INC	CX
		CMP	CL,15
		JBE	L_0102
		MOV	CL,15
L_0102:
		MOV	D_5E,CL
		POP	AX
		MOV	D_54,0
		TEST	AH,20H
		JNZ	L_0103
		MOV	D_54,-1
		XOR	BP,BP
		CALL	S_XSGB			;显示光标
L_0103:
		STI
		JMP	L_RET

;AH=2 光标定位
L_AH02:
		CMP	DH,19H			;判范围
		JAE	L_RET
		CMP	DL,D_4A
		JAE	L_RET
		MOV	ES:[450H],DX

		MOV	BP,1
		CALL	S_XSGB			;关闭光标

	;	CMP	DH,18H
	;	JNZ	L_1B41
	;	JMP	SHORT L_1B55
L_1B41:
		XOR	BH,BH
		MOV	AL,D_A5
		CMP	DH,AL
		JB	L_1B66
		MOV	AL,D_A6
		CMP	AL,DH
		JB	L_1BAC
		SUB	DH,D_A5
L_1B55:
		MOV	D_50,DX

		XOR	BP,BP
		CALL	S_XSGB
		JMP	L_RET
L_1B66:
		PUSH	DX
		SUB	AL,DH
		CMP	AL,E_HS 		;9
		JB	L_1B6F
		MOV	AL,E_HS 		;9
L_1B6F:
		XOR	CX,CX
		MOV	DH,E_HS 		;9
		MOV	DL,D_4A
		DEC	DL
		PUSH	AX
		CALL	S_213D
		POP	BX
		MOV	BH,BL
		DEC	BH
		POP	DX
		PUSH	DX
		MOV	AX,DX
		MOV	D_A5,AH
		ADD	AH,E_HS 		;9
		MOV	D_A6,AH
		ADD	DH,BH
L_1B93:
		XOR	DL,DL
		MOV	DI,DX
		CALL	S_1BF7
		CMP	BH,0
		JZ	L_1BA7
		DEC	BH
		MOV	DX,DI
		DEC	DH
		JMP	SHORT L_1B93
L_1BA7:
		POP	DX
		XOR	DH,DH
		JMP	SHORT L_1B55
L_1BAC:
		PUSH	DX
		SUB	DH,AL
		MOV	AL,DH
		CMP	AL,E_HS 		;9
		JB	L_1BB7
		MOV	AL,E_HS 		;9
L_1BB7:
		XOR	CX,CX
		MOV	DH,E_HS 		;9
		MOV	DL,D_4A
		DEC	DL
		PUSH	AX
		CALL	S_204B
		POP	BX
		DEC	BL
		MOV	BH,E_HS 		;9
		SUB	BH,BL
		POP	DX
		MOV	AX,DX
		PUSH	DX
		MOV	D_A6,AH
		SUB	AH,E_HS 		;9
		MOV	D_A5,AH
		SUB	DH,BL
L_1BDD:
		XOR	DL,DL
		MOV	DI,DX
		CALL	S_1BF7
		CMP	BH,E_HS 		;9
		JZ	L_1BF1
		INC	BH
		MOV	DX,DI
		INC	DH
		JMP	SHORT L_1BDD
L_1BF1:
		POP	DX
		MOV	DH,E_HS 		;9
		JMP	L_1B55
;子程序:
S_1BF7		PROC	NEAR
		PUSH	D_71
		MOV	CX,1
		XOR	BL,BL
		MOV	D_50,BX
L_1C19:
		MOV	AX,DI
		CALL	S_1D11
		SAR	AX,1
		MOV	SI,AX
		MOV	AL,DS:[SI+D_ZFQ]
		MOV	BL,DS:[SI+D_SXQ]
		MOV	AH,0AH
		INT	10H
		INC	DI
		MOV	AX,D_50
		INC	AL
		CMP	AL,D_4A
		JB	L_1C4F
		POP	D_71
		RET
L_1C4F:
		MOV	D_50,AX
		JMP	SHORT L_1C19
S_1BF7		ENDP

;子程序:显示光标
S_XSGB		PROC	NEAR
		TEST	D_54,0FFH
		JZ	L_1B02
		OR	BP,BP
		JZ	L_1B00
		CMP	D_52,0FFH
		JZ	L_1B10
		RET
L_1B00:
		CMP	D_52,0
		JZ	L_1B10
L_1B02:
		RET
L_1B10:
		NOT	D_52
		CALL	S_SPDZ0
		CMP	D_49,6
		JZ	L_1B17
		SHL	DI,1
L_1B17:
		MOV	AL,D_5E
S_XSGB1:
		ADD	DI,8*80 		;**
L_1B20:
		NOT	BYTE PTR ES:[DI+2000H]
		CMP	D_49,6
		JZ	L_1B22
		NOT	BYTE PTR ES:[DI+2001H]
L_1B22:
		DEC	AL
		JZ	L_1B30
		NOT	BYTE PTR ES:[DI]
		CMP	D_49,6
		JZ	L_1B24
		NOT	BYTE PTR ES:[DI+1]
L_1B24:
		DEC	AL
		JZ	L_1B30
		SUB	DI,80
		JMP	SHORT L_1B20
L_1B30:
		RET
S_XSGB		ENDP

;AH=3 取光标位置
L_AH03:
		MOV	DX,D_50
		CMP	DH,18H
		JA	L_1C77
		ADD	DH,D_A5
L_1C77:
		MOV	CX,607H
		POP	AX
		POP	BX
		POP	DI
		POP	DI
		JMP	L_RET3
;子程序:
S_1CFA		PROC	NEAR
		CMP	AH,18H
		JA	S_1D11
		ADD	AH,D_A5
S_1D11:
		PUSH	BX
		MOV	BX,AX
		MOV	AL,AH
		MUL	D_4A
		XOR	BH,BH
		ADD	AX,BX
		SHL	AX,1
		POP	BX
		RET
S_1CFA		ENDP

;子程序:
S_1D22		PROC	NEAR
		CMP	CH,17H
		JB	L_1D29
		MOV	CH,17H
L_1D29:
		CMP	DH,18H
		JB	L_1D30
		MOV	DH,18H
L_1D30:
		CMP	CL,D_4A
		JB	L_1D3C
		MOV	CL,D_4A
		DEC	CL
L_1D3C:
		CMP	DL,D_4A
		JB	L_1D48
		MOV	DL,D_4A
		DEC	DL
L_1D48:
		RET
S_1D22		ENDP

;AH=6 上滚
L_AH06:
		CALL	S_1D22
		MOV	BL,AL
		XOR	BH,BH
		PUSH	AX
		PUSH	BX
		PUSH	CX
		PUSH	DX

		PUSH	AX
	;	MOV	AX,1404H		;关闭提示行
	;	INT	10H
		MOV	BP,1
		CALL	S_XSGB			;关闭光标
		POP	AX

		MOV	BL,AL
		PUSH	BX
		MOV	AX,CX
		CALL	S_1FE0
		JZ	L_1FBE
		ADD	SI,AX
		MOV	AH,DH
		SUB	AH,BL
		JNZ	L_1F7C
		POP	AX
		JMP	SHORT L_1F93
L_1F7C:
		CALL	S_2001
		ADD	SI,BP
		ADD	DI,BP
		DEC	AH
		JNZ	L_1F7C
L_1F87:
		POP	AX
		MOV	AL,20H
L_1F8A:
		CALL	S_2032
		ADD	DI,BP
		DEC	BL
		JNZ	L_1F8A
L_1F93:
		POP	DX
		POP	CX
		POP	BX
		POP	AX

		CMP	D_A6,CH
		JB	L_1FD3
		MOV	AH,D_A5
		CMP	DH,AH
		JB	L_1FD3
		CMP	AH,CH
		JB	L_1FAF
		XOR	CH,CH
		JMP	SHORT L_1FB1
L_1FAF:
		SUB	CH,AH
L_1FB1:
		MOV	AH,D_A6
		CMP	DH,AH
		JB	L_1FC2
		MOV	DH,E_HS 		;9
		JMP	SHORT L_1FC6
L_1FBE:
		MOV	BL,DH
		JMP	SHORT L_1F87
L_1FC2:
		SUB	DH,D_A5
L_1FC6:
		CMP	AL,E_HS 		;9
		JB	L_1FCC
		MOV	AL,E_HS 		;9
L_1FCC:
		CMP	CH,DH
		JZ	L_1FD3
		CALL	S_204B
L_1FD3:
		JMP	L_RET
;子程序:
S_1FE0		PROC	NEAR
		CALL	S_1D11
		SAR	AX,1
		MOV	DI,AX
		MOV	SI,AX
		SUB	DX,CX
		INC	DH
		INC	DL
		XOR	CH,CH
		MOV	BP,WORD PTR D_4A
		MOV	AL,BL
		MUL	D_4A
		PUSH	DS
		POP	ES
		CMP	BL,0
		RET
S_1FE0		ENDP

;子程序:
S_2001		PROC	NEAR
		MOV	CL,DL
		PUSH	SI
		PUSH	DI
		ADD	SI,D_ZFQ
		ADD	DI,D_ZFQ
		REP	MOVSB
		POP	DI
		POP	SI
		PUSH	SI
		PUSH	DI
		ADD	SI,D_BZQ
		ADD	DI,D_BZQ
		MOV	CL,DL
		REP	MOVSB
		POP	DI
		POP	SI
		PUSH	SI
		PUSH	DI
		ADD	SI,D_SXQ
		ADD	DI,D_SXQ
		MOV	CL,DL
		REP	MOVSB
		POP	DI
		POP	SI
		RET
S_2001		ENDP

;子程序:
S_2032		PROC	NEAR
		MOV	CL,DL
		PUSH	DI
		ADD	DI,D_ZFQ
		REP	STOSB
		POP	DI
		PUSH	DI
		ADD	DI,D_BZQ
		PUSH	AX
		XOR	AL,AL
		MOV	CL,DL
		REP	STOSB
		POP	AX
		POP	DI
		RET
S_2032		ENDP

;子程序:
S_204B		PROC	NEAR
		CMP	CH,DH
		JB	L_2050
		RET
L_2050:
		PUSH	DS
		MOV	BL,AL
		MOV	AX,CX
		CALL	S_SPDZ

		SUB	DX,CX
		ADD	DX,101H
		MOV	AL,E_HLINE
		MUL	DH
		MOV	DH,AL
		CMP	D_49,6			;**
		JAE	L_2075
		SHL	DL,1
		SHL	DI,1
L_2075:
		PUSH	ES
		POP	DS
		SUB	CH,CH
		MOV	AL,E_HLINE
		MUL	BL
		OR	AX,AX
		JZ	L_20AD
		MOV	BL,AL
		MOV	AH,80
		MUL	AH
		MOV	SI,DI
		ADD	SI,AX
		MOV	AH,DH
		SUB	AH,BL
L_208F:
		CALL	S_21AA
		SUB	SI,1FB0H
		SUB	DI,1FB0H
		DEC	AH
		JNZ	L_208F
L_209E:
		MOV	AL,BH
L_20A0:
		CALL	S_21C3
		SUB	DI,1FB0H
		DEC	BL
		JNZ	L_20A0
		POP	DS
		RET
L_20AD:
		MOV	BL,DH
		JMP	SHORT L_209E
S_204B		ENDP

;AH=7 下滚
L_AH07:
		CALL	S_1D22
		MOV	BL,AL
		XOR	BH,BH
		PUSH	AX
		PUSH	BX
		PUSH	CX
		PUSH	DX

		PUSH	AX
	;	MOV	AX,1404H		;关闭提示行
	;	INT	10H
		MOV	BP,1
		CALL	S_XSGB			;关闭光标
		POP	AX

		STD
		MOV	BL,AL
		PUSH	BX
		MOV	AX,DX
		CALL	S_1FE0
		JZ	L_2112
		SUB	SI,AX
		MOV	AH,DH
		SUB	AH,BL
		JNZ	L_20DD
		POP	AX
		JMP	SHORT L_20F4
L_20DD:
		CALL	S_2001
		SUB	SI,BP
		SUB	DI,BP
		DEC	AH
		JNZ	L_20DD
L_20E8:
		POP	AX
		MOV	AL,20H
L_20EB:
		CALL	S_2032
		SUB	DI,BP
		DEC	BL
		JNZ	L_20EB
L_20F4:
		POP	DX
		POP	CX
		POP	BX
		POP	AX

		CMP	D_A6,CH
		JB	L_2130
		CMP	DH,D_A5
		JB	L_2130
		MOV	AH,D_A5
		CMP	AH,CH
		JB	L_2116
		XOR	CH,CH
		JMP	SHORT L_2118
L_2112:
		MOV	BL,DH
		JMP	SHORT L_20E8
L_2116:
		SUB	CH,AH
L_2118:
		MOV	AH,D_A6
		CMP	DH,AH
		JB	L_2125
		MOV	DH,E_HS 		;9
		JMP	SHORT L_2127
L_2125:
		SUB	AH,DH
L_2127:
		CMP	AL,E_HS 		;9
		JB	L_212D
		MOV	AL,E_HS 		;9
L_212D:
		CALL	S_213D
L_2130:
		JMP	L_RET
;子程序:
S_213D		PROC	NEAR
		CMP	CH,DH
		JB	L_2142
		RET
L_2142:
		PUSH	DS
		STD
		MOV	BL,AL
		MOV	AX,DX
		CALL	S_SPDZ

		SUB	DX,CX
		ADD	DX,101H
		MOV	AL,E_HLINE
		MUL	DH
		MOV	DH,AL
		CMP	D_49,6			;**
		JAE	L_2169
		SHL	DL,1
		SHL	DI,1
		INC	DI
L_2169:
		PUSH	ES
		POP	DS
		SUB	CH,CH
		ADD	DI,280H
		MOV	AL,E_HLINE
		MUL	BL
		OR	AX,AX
		JZ	L_21A6
		MOV	BL,AL
		MOV	AH,80
		MUL	AH
		MOV	SI,DI
		SUB	SI,AX
		MOV	AH,DH
		SUB	AH,BL
L_2187:
		CALL	S_21AA
		SUB	SI,2050H
		SUB	DI,2050H
		DEC	AH
		JNZ	L_2187
L_2196:
		MOV	AL,BH
L_2198:
		CALL	S_21C3
		SUB	DI,2050H
		DEC	BL
		JNZ	L_2198
		CLD
		POP	DS
		RET
L_21A6:
		MOV	BL,DH
		JMP	SHORT L_2196
S_213D		ENDP

;子程序:
S_21AA		PROC	NEAR
		MOV	CL,DL
		PUSH	SI
		PUSH	DI
		REP	MOVSB
		POP	DI
		POP	SI
		ADD	SI,2000H
		ADD	DI,2000H
		PUSH	SI
		PUSH	DI
		MOV	CL,DL
		REP	MOVSB
		POP	DI
		POP	SI
		RET
S_21AA		ENDP

;子程序:
S_21C3		PROC	NEAR
		MOV	CL,DL
		PUSH	DI
		REP	STOSB
		POP	DI
		ADD	DI,2000H
		PUSH	DI
		MOV	CL,DL
		REP	STOSB
		POP	DI
		RET
S_21C3		ENDP

;AH=8 取光标处字符
L_AH08:
		MOV	AX,D_50
		CALL	S_1CFA
		SHR	AX,1
		MOV	SI,AX
		MOV	AL,DS:[SI+D_ZFQ]
		MOV	AH,DS:[SI+D_SXQ]
		POP	BX
		JMP	L_RET8

;AH=9,0AH 显示字符
L_AH09:
		PUSH	AX
		MOV	BP,1
		CALL	S_XSGB
		POP	AX

		CMP	AL,80H
		JB	L_2219
		CMP	AL,0A0H
		JA	L_2219
L_2209:
		MOV	DI,D_50
		CALL	S_22A2
		JMP	SHORT L_2242
L_2219:
		CMP	AL,0FFH
		JZ	L_2209
		CALL	S_25DF
		MOV	BP,AX
		TEST	AH,7
		JNZ	L_222B
		JMP	SHORT L_2242
L_222B:
		TEST	AH,1
		JNZ	L_2233
		JMP	SHORT L_2239
L_2233:
		CALL	S_22A2
		JMP	SHORT L_2242
L_2239:
		MOV	SI,DX
		MOV	DI,D_71
		CALL	S_224B
L_2242:
		JMP	L_RET
;子程序:显示汉字
S_224B		PROC	NEAR
		PUSH	DI
		MOV	DI,OFFSET D_78
		PUSH	CS
		POP	ES
		MOV	DS,SI
		XOR	SI,SI
		MOV	CX,16
L_225A:
		LODSW
		TEST	BL,70H
		JZ	L_226C
		NOT	AX
L_226C:
		STOSB
		MOV	ES:[DI+15],AH
		LOOP	L_225A
		PUSH	CS
		POP	DS
		POP	DI

		PUSH	DI
		MOV	AX,DI
		CALL	S_SPDZ
		MOV	SI,OFFSET D_78
		MOV	CL,1
		CALL	S_230D
		ADD	SI,16
		POP	AX
		INC	AL
		CMP	AL,D_4A
		JB	L_2296
		XOR	AL,AL
		INC	AH
L_2296:
		CALL	S_SPDZ
		MOV	CL,1
		CALL	S_230D
		RET
S_224B		ENDP

;子程序:显示字符
S_22A2		PROC	NEAR
		MOV	D_HZ9,0
		CMP	AL,80H
		JB	L_22B0
		SUB	AL,80H
L_22B0:
		MOV	SI,0F000H
		MOV	DS,SI
		MOV	AH,8
		MUL	AH
		ADD	AX,0FA6EH		;ROM BIOS 字符库
		MOV	SI,AX

		PUSH	DI
		PUSH	CX
		MOV	DI,OFFSET D_78
		PUSH	CS
		POP	ES

		MOV	CX,2
		XOR	AX,AX
		TEST	BL,70H
		JZ	L_22D6
		NOT	AX
L_22D6:
		REP	STOSW

		MOV	CL,4
L_22DB:
		LODSB
		TEST	BL,70H
		JZ	L_22E3
		NOT	AX
L_22E3:
		STOSB
		LODSB
		TEST	BL,70H
		JZ	L_22ED
		NOT	AX
L_22ED:
		STOSB
		STOSB
		LOOP	L_22DB
		POP	CX
		POP	DI
		PUSH	CS
		POP	DS

		MOV	AX,DI
		CMP	AH,18H
		JNZ	L_2304
		INC	BH
L_2304:
		CALL	S_SPDZ
		MOV	SI,OFFSET D_78
L_2305:
		PUSH	DI
		CALL	S_230D
		POP	DI
		INC	DI
		CMP	D_49,6
		JZ	L_2306
		INC	DI
L_2306:
		LOOP	L_2305
		RET
S_22A2		ENDP

;子程序:字模显示
S_230D		PROC	NEAR
		CMP	D_49,6			;**
		JB	L_2346
		PUSH	SI
		MOV	DH,8
L_231C:
		LODSB
		TEST	BL,80H
		JNZ	L_233A
		STOSB
		LODSB
L_2324:
		MOV	ES:[DI+1FFFH],AL
		ADD	DI,4FH
		DEC	DH
		JNZ	L_231C
		POP	SI

		CMP	D_5D16,18		;18线处理?
		JNZ	L_2336
		CMP	D_HZ9,0A9H		;判九区制表符?
		JZ	L_2330
		XOR	AL,AL
L_2330:
		STOSB
		MOV	ES:[DI+1FFFH],AL
L_2336:
		RET
L_233A:
		XOR	AL,ES:[DI]
		STOSB
		LODSB
		XOR	AL,ES:[DI+1FFFH]
		JMP	SHORT L_2324
L_2346:
		MOV	DL,BL
		SHL	DI,1
		CALL	S_238B
L_234E:
		PUSH	SI
		MOV	DH,8
L_2353:
		LODSB
		CALL	S_23A2
		AND	AX,BX
		TEST	DL,80H
		JZ	L_2361
		XOR	AX,ES:[DI]
L_2361:
		MOV	ES:[DI],AX
		LODSB
		CALL	S_23A2
		AND	AX,BX
		TEST	DL,80H
		JZ	L_2374
		XOR	AX,ES:[DI+2000H]
L_2374:
		MOV	ES:[DI+2000H],AX
		ADD	DI,50H
		DEC	DH
		JNZ	L_2353
		POP	SI

		CMP	D_5D16,18		;18线处理?
		JNZ	L_2376
		CMP	D_HZ9,0A9H		;判九区制表符?
		JZ	L_2370
		XOR	AX,AX
L_2370:
		STOSW
		MOV	ES:[DI+1FFFH],AX
L_2376:
		RET
S_230D		ENDP

;子程序:
S_238B		PROC	NEAR
		PUSH	AX
		AND	BL,3
		MOV	AL,BL
		PUSH	CX
		MOV	CX,3
L_2395:
		SHL	AL,1
		SHL	AL,1
		OR	BL,AL
		LOOP	L_2395
		MOV	BH,BL
		POP	CX
		POP	AX
		RET
S_238B		ENDP

;子程序:
S_23A2		PROC	NEAR
		PUSH	DX
		PUSH	CX
		MOV	DL,AL
		MOV	DH,AL
		MOV	CX,8
L_23AB:
		RCL	DL,1
		RCL	AX,1
		RCL	DH,1
		RCL	AX,1
		LOOP	L_23AB
		POP	CX
		POP	DX
		XCHG	AH,AL
		RET
S_23A2		ENDP

;子程序:
S_SPDZ0 	PROC	NEAR
		MOV	AX,D_50
;子程序:
S_SPDZ:
		PUSH	BX
		PUSH	CX
		PUSH	DX
		MOV	CH,AH
		CMP	AH,18H
		JB	L_SPDZ1
		MOV	AH,E_HS+1
L_SPDZ1:
		MOV	BX,AX
		MOV	AL,AH
		CBW
		MOV	DX,E_HBYTE
		MUL	DX
		XOR	BH,BH
		ADD	AX,BX

		CMP	CH,18H
		JB	L_SPDZ2
		ADD	AX,50H
L_SPDZ2:
		MOV	DI,AX
		MOV	AX,0B800H		;**
		MOV	ES,AX
		POP	DX
		POP	CX
		POP	BX
		RET
S_SPDZ0 	ENDP

;AH=0EH 显示字符并推进光标
L_AH0E:
		PUSH	AX
		MOV	AH,3
		INT	10H
		POP	AX

		PUSH	AX
		CMP	AL,8
		JZ	L_2476
		CMP	AL,0DH
		JZ	L_247F
		CMP	AL,0AH
		JZ	L_2483
		CMP	AL,7
		JZ	L_248A
		MOV	BH,0
		MOV	AH,0AH
		MOV	CX,1
		INT	10H
		INC	DL
		CMP	DL,D_4A
		JNZ	L_2472
		MOV	DL,0
		CMP	DH,18H
		JB	L_2470
L_243E:
		MOV	AH,2
		MOV	BH,0
		INT	10H
		MOV	AX,601H
		MOV	CX,0
		MOV	DH,18H
		MOV	DL,D_4A
		DEC	DL
L_2465:
		INT	10H
L_2467:
		POP	AX
		JMP	L_RET
L_2470:
		INC	DH
L_2472:
		MOV	AH,2
		JMP	SHORT L_2465
L_2476:
		CMP	DL,0
		JZ	L_2472
		DEC	DL
		JMP	SHORT L_2472
L_247F:
		MOV	DL,0
		JMP	SHORT L_2472
L_2483:
		CMP	DH,18H
		JNZ	L_2470
		JMP	SHORT L_243E
L_248A:
		MOV	AX,0E07H
		CALL	S_INT10
		JMP	SHORT L_2467

;AH=13H 显示字符串 ES:BP=字符串首址, DX=位置, CX=串长
;		   AL=0 BL=属性 显示后光标不变
;		   AL=1 BL=属性 显示后光标移串尾
;		   AL=2 串含属性 显示后光标不变
;		   AL=3 串含属性 显示后光标移串尾
;		   AL=4 BL=属性 串以0结尾 显示后光标移串尾
;		   AL=5 BL=新属性 DX=串首位置 CX=串长 显示后原光标位置不变
L_AH13:
		CMP	AL,4			;方式=4?
		JZ	L_1302
L_1301:
		OR	CX,CX			;串长>0?
		JNZ	L_1302
		JMP	L_1308
L_1302:
		MOV	SI,D_50 	   ;取当前光标位置
		PUSH	AX
		MOV	AH,2			;光标定位
		INT	10H
		POP	AX
L_1303:
		PUSH	CX
		PUSH	BX
		PUSH	AX
		MOV	AH,AL			;方式号
		CMP	AL,5
		JNZ	L_1303A
		MOV	AH,8			;取当前光标位置字符
		INT	10H
		JMP	SHORT L_1303B
L_1303A:
		MOV	AL,ES:[BP]		;取一字符
		INC	BP
		CMP	AX,400H 		;方式4:AL=0(结束)
		JZ	L_1308A
L_1303B:
		CMP	AL,8			;是退格?
		JZ	L_1304
		CMP	AL,0DH			;是回车?
		JZ	L_1304
		CMP	AL,0AH			;是换行?
		JZ	L_1304
		CMP	AL,7			;是响铃?
		JNZ	L_1305
L_1304:
		MOV	AH,0EH			;执行
		INT	10H
		MOV	DX,D_50 	   ;取当前光标位置
		JMP	SHORT L_1307
L_1305:
		MOV	CX,1
		CMP	AH,4
		JNB	L_1306
		CMP	AH,2			;方式=2,3?
		JB	L_1306
		MOV	BL,ES:[BP]		;取属性
		INC	BP
L_1306:
		MOV	AH,9			;显示字符
		INT	10H
		INC	DL			;列位置+1
		CMP	DL,80			;判行尾?
		JB	L_1307
		XOR	DL,DL			;行首
		INC	DH			;行+1
		CMP	DH,25			;屏尾?
		JB	L_1307
		DEC	DH
L_1307:
		MOV	AH,2			;光标定位
		INT	10H
		POP	AX
		POP	BX
		POP	CX
		CMP	AL,4
		JZ	L_1303
		LOOP	L_1303
		CMP	AL,1			;方式=1
		JZ	L_1308
		CMP	AL,3			;方式=3
		JZ	L_1308
L_1307A:
		MOV	DX,SI			;恢复原光标
		MOV	AH,2
		INT	10H
L_1308:
		JMP	L_RET
L_1308A:
		POP	AX
		POP	BX
		POP	CX
		JMP	SHORT L_1308

;AH=14H ********
;AH=14H 提示行管理 AL=0 清提示行
;		   AL=1 显示字符DL CL=个数
;		   AL=2 提示行光标定位DL
;		   AL=3 显示字符DL并推进光标
;		   AL=4 关闭提示行
;		   AL=6 取显示参数 AL=显示方式,AH=最大色号
;				   BH=提示行浮动位置
;				   CL=显示行数,CH=每行扫描线数
;				   DL=提示行光标位置 DH=当前提示行位置
;				   SI=横向点数 DI=纵向扫描线数
;				   BP=显示缓冲区段
;		   AL=8 位置DX处字符反相显示
;		   AL=10 在指定位置DL显示BL属性2.13标志
L_AH14:
		OR	AL,AL			;<打开提示行>
		JNZ	L_1410
		MOV	D_56,AL
		MOV	AL,0FFH
L_1401:
		MOV	DI,0B800H		;**
		MOV	ES,DI
		MOV	DI,E_WBYTE+2000H
		MOV	CL,80
		REP	STOSB
		MOV	DI,E_WBYTE
		MOV	CL,80
		REP	STOSB

		XOR	AL,AL
		MOV	CX,E_HBYTE
L_1402:
		STOSB
		MOV	ES:[DI+1FFFH],AL
		LOOP	L_1402
		JMP	L_RET
L_1410:
		CMP	AL,1			;<显示,不移光标>
		JNZ	L_1420
		CALL	S_253E			;子程序:提示行显示字符
		JMP	L_RET
L_1420:
		MOV	CX,1
		CMP	AL,2			;<提示行光标定位>
		JNZ	L_1430
		PUSH	DX
		MOV	DL,' '
		CALL	S_253D
		POP	DX
		CMP	DL,80			;			10.4
		JAE	L_1421
		MOV	D_56,DL
		CALL	S_253C			;子程序:提示行光标
L_1421:
		JMP	L_RET
L_1430:
		CMP	AL,3			;<显示并推进光标>
		JNZ	L_1440
		CMP	DL,8
		JNZ	L_1432
		MOV	DL,' '
		CALL	S_253D
		MOV	AL,D_56
		CMP	AL,0
		JZ	L_1431
		DEC	AL
L_1431:
		MOV	D_56,AL
		JMP	SHORT L_1435
L_1432:
		CMP	DL,7
		JNZ	L_1433
		MOV	AX,0E07H
		CALL	S_INT10
		JMP	L_RET
L_1433:
		CALL	S_253E			;子程序:提示行显示字符
		MOV	AL,D_56
		CMP	AL,4FH
		JA	L_1434
		INC	AL
L_1434:
		MOV	D_56,AL
L_1435:
		CALL	S_253C			;子程序:提示行光标
		JMP	L_RET
L_1440:
		CMP	AL,4			;<关闭提示行>
		JNZ	L_1460
		XOR	AL,AL
		JMP	L_1401
L_1460:
		CMP	AL,6			;<取显示参数>
		JNZ	L_1480
		MOV	AX,106H 		;显示方式和最大色号
		MOV	CX,120BH		;取每行扫描线数和显示行数
		XOR	BX,BX			;提示行浮动位置
		MOV	DX,WORD PTR D_56	;提示行当前光标位置
		MOV	SI,639			;横向点数
		MOV	DI,199			;纵向扫描线数
		MOV	BP,0B800H
		POP	ES
		POP	ES
		POP	ES
		POP	ES
		POP	ES
		POP	ES
		POP	ES
		JMP	L_RET1406
L_1480:
		CMP	AL,8			;<位置DX处字符反相>
		JNZ	L_14A0
		MOV	AX,DX
		CALL	S_SPDZ
		MOV	AL,16
		CALL	S_XSGB1
		JMP	L_RET
L_14A0:
		CMP	AL,10			;<显示2.13标志> 	10.4
		JNZ	L_14A9
		PUSH	WORD PTR D_56
		MOV	BX,4E4EH
		MOV	D_56,4CH
		MOV	DL,0AFH
		CALL	S_253E
		INC	D_56
		MOV	DL,0A1H
		CALL	S_253E
		INC	D_56
		MOV	DL,0AFH
		CALL	S_253E
		INC	D_56
		MOV	DL,0A2H
		CALL	S_253E
		POP	WORD PTR D_56
L_14A9:
		JMP	L_RET

;子程序:提示行光标
S_253C		PROC	NEAR
		MOV	DL,'_'
S_253D:
		MOV	BL,7
		MOV	CX,1
;子程序:提示行显示字符
S_253E:
		MOV	DI,0B800H		;**
		MOV	ES,DI
		MOV	DI,E_WBYTE+80		;**			10.4
		MOV	AX,WORD PTR D_56
		ADD	DI,AX

		MOV	AL,DL
		CMP	AL,0A0H
		JA	L_258F
		MOV	AH,8
		MUL	AH
		ADD	AX,0FA6EH
		MOV	SI,AX
		MOV	AX,0F000H
		MOV	DS,AX
L_2550:
		PUSH	CX
		PUSH	DI
		PUSH	SI
		MOV	CX,4
		XOR	AX,AX
		TEST	BL,60H
		JZ	L_2560
		NOT	AX
L_2560:
		STOSB
		MOV	ES:[DI+1FFFH],AL
		ADD	DI,4FH
		LOOP	L_2560
		MOV	CL,4
L_2581:
		LODSB
		TEST	BL,60H
		JZ	L_2588
		NOT	AL
L_2588:
		STOSB
		LODSB
		TEST	BL,60H
		JZ	L_258A
		NOT	AL
L_258A:
		MOV	ES:[DI+1FFFH],AL
		ADD	DI,4FH
		LOOP	L_2581
		POP	SI
		POP	DI
		INC	DI
		POP	CX
		LOOP	L_2550
		PUSH	CS
		POP	DS
		RET
L_258F:
		CMP	D_A9,0
		JNZ	L_259D
		MOV	D_A9,AL
		RET
L_259D:
		MOV	DH,D_A9
		MOV	D_A9,0
		DEC	DI			;			10.4
		CALL	S_276C
		MOV	DS,DX
		XOR	SI,SI
		MOV	CX,8
L_25AF:
		LODSW
		TEST	BL,60H
		JZ	L_25B0
		NOT	AX
L_25B0:
		STOSW
		LODSW
		TEST	BL,60H
		JZ	L_25B2
		NOT	AX
L_25B2:
		MOV	ES:[DI+1FFEH],AX
		ADD	DI,4EH
		LOOP	L_25AF
		PUSH	CS
		POP	DS
		RET
S_253C		ENDP

;AH=15H 显示光标
L_AH15:
		CBW
		MOV	BP,AX
		CALL	S_XSGB
		JMP	L_RET

;AH=16H 取汉字或字符点阵
L_AH16:
		OR	DH,DH			;判汉字?
		JZ	L_1602
		CALL	S_276C
		MOV	DS,DX
		XOR	SI,SI
		MOV	ES,BP
		MOV	DI,BX
		MOV	CX,16
L_1601:
		LODSW
		STOSB
		MOV	ES:[DI+15],AH
		LOOP	L_1601
		PUSH	CS
		POP	DS
		JMP	L_RET
L_1602:
		MOV	AL,8
		MUL	DL
		ADD	AX,0FA6EH
		MOV	SI,AX
		MOV	AX,0F000H
		MOV	DS,AX
		MOV	ES,BP			;目标段
		MOV	DI,BX			;    偏移
		XOR	AL,AL
		MOV	CX,8
		REP	STOSB
		MOV	CX,8
		REP	MOVSB
		JMP	L_RET

;AH=17H 光标控制
L_AH17:
		MOV	D_54,AL
		MOV	BP,1
		CALL	S_XSGB
		JMP	L_RET

;AH=18H 	AL=0识别汉字		AL=1不识汉字
;		AL=4不允许设置显示方式	AL=5允许
;		AL=13光标大小控制 BH=1由AH=1控制*;BH=0,BL=光标大小
;		AL=17繁体		AL=18简体
;		AL=24设置18线方式*	AL=25恢复16线方式
L_AH18:
		OR	AL,AL			;<识别汉字>
		JNZ	L_1801
		MOV	AL,74H			;JZ
L_1801A:
		MOV	BYTE PTR K_HZ,AL
		JMP	L_RET
L_1801:
		CMP	AL,1			;<不识汉字>
		JNZ	L_1804
		MOV	AL,0EBH 		;JMP SHORT
		JMP	SHORT L_1801A
L_1804:
		CMP	AL,4			;<不允许设置显示方式>
		JNZ	L_1805
		MOV	D_AH,OFFSET L_RET
		JMP	L_RET
L_1805:
		CMP	AL,5			;<允许设置显示方式>
		JNZ	L_1813
		MOV	DS:D_AH,OFFSET L_AH00
		JMP	L_RET
L_1813: 					;<光标大小控制>
		CMP	AL,13
		JNZ	L_1817
		OR	BH,BH
		JZ	L_18131
		MOV	D_AH01,OFFSET L_AH01
		RET
L_18131:
		MOV	D_AH01,OFFSET L_RET
		MOV	D_5E,BL
		RET
L_1817:
		CMP	AL,17			;<繁体>
		JNZ	L_1818
L_18171:
		SUB	AL,17
		MOV	D_0069,AL
		JMP	L_RET
L_1818:
		CMP	AL,18			;<简体>
		JZ	L_18171
L_1824: 					;<设置18线方式>
		CMP	AL,24
		JNZ	L_1825
		MOV	AL,18
L_1824A:
		MOV	D_5D16,AL
L_1824B:
		JMP	L_RET
L_1825: 					;<恢复16线方式>
		CMP	AL,25
		JNZ	L_1824B
		MOV	AL,16
		JMP	SHORT L_1824A
;子程序:
S_25DF		PROC	NEAR
		XOR	DX,DX
		PUSH	CX
		PUSH	BX
		MOV	BP,D_50
		TEST	AL,80H
K_HZ	EQU	$
		JZ	L_2605
		JMP	SHORT L_2606
L_2605:
		PUSH	AX
		MOV	AX,BP
		CALL	S_275A
		POP	AX
		POP	BX
		POP	CX
		MOV	DS:[DI+D_ZFQ],AL
		MOV	DS:[DI+D_SXQ],BL
		MOV	BYTE PTR DS:[DI+D_BZQ],0
		MOV	DI,BP
		MOV	AH,1
		RET
L_2606:
		PUSH	AX
		CALL	S_26E6
		POP	AX
		OR	DL,DL
		JZ	L_2627
		DEC	DL
		JZ	L_267E
		CALL	S_26C5
		JNZ	L_2622
		MOV	DH,81H
L_261A:
		MOV	CX,AX
		POP	BX
		CALL	S_26FD
		POP	CX
		RET
L_2622:
		CALL	S_26D1
		JMP	SHORT L_261A
L_2627:
		CMP	BYTE PTR D_50+1,0
		JNZ	L_2635
		CMP	D_A5,0
		JZ	L_261A
L_2635:
		PUSH	AX
		MOV	AX,BP
		DEC	AH
		MOV	AL,D_4A
		DEC	AL
		CALL	S_2762
		JZ	L_2647
		POP	AX
		JMP	SHORT L_261A
L_2647:
		MOV	AX,BP
		DEC	AH
		MOV	AL,D_4A
		DEC	AL
		MOV	D_71,AX
		CALL	S_275A
		POP	CX
		POP	BX
		MOV	DS:[DI+D_ZFQ+1],CL
		MOV	WORD PTR DS:[DI+D_BZQ],201H
		MOV	DS:[DI+D_SXQ+1],BL
		MOV	DL,CL
		MOV	DH,DS:[DI+D_ZFQ]
		MOV	AX,BP
		CMP	AH,0
		JZ	L_267A
		CALL	S_276C
		MOV	AH,2
		POP	CX
		RET
L_267A:
		XOR	AH,AH
		POP	CX
		RET
L_267E:
		PUSH	AX
		CALL	S_26C5
		JNZ	L_268F
		MOV	AX,BP
		MOV	DH,81H
		POP	CX
		POP	BX
		CALL	S_26FD
		POP	CX
		RET
L_268F:
		MOV	AX,BP
		CMP	AH,E_HS 		;9
		JNZ	L_26A6
		CMP	D_A6,18H
		JB	L_26A6
L_269D:
		POP	CX
		POP	BX
		XOR	DH,DH
		CALL	S_26FD
		POP	CX
		RET
L_26A6:
		MOV	AX,BP
		XOR	AL,AL
		INC	AH
		CALL	S_2762
		JZ	L_26B3
		JMP	SHORT L_269D
L_26B3:
		POP	CX
		POP	BX
		MOV	DH,82H
		CALL	S_26FD
		MOV	CX,BP
		CMP	CH,E_HS 		;9
		JNZ	L_26C3
		XOR	AH,AH
L_26C3:
		POP	CX
		RET
S_25DF		ENDP

;子程序:
S_26C5		PROC	NEAR
		PUSH	BX
		PUSH	AX
		MOV	AX,BP
		DEC	AL
		CALL	S_2762
		POP	AX
		POP	BX
		RET
S_26C5		ENDP

;子程序:
S_26D1		PROC	NEAR
		PUSH	BX
		PUSH	AX
		MOV	AX,BP
		INC	AL
		CALL	S_2762
		JNZ	L_26E1
		MOV	DH,82H
		JMP	SHORT L_26E3
L_26E1:
		XOR	DH,DH
L_26E3:
		POP	AX
		POP	BX
		RET
S_26D1		ENDP

;子程序:
S_26E6		PROC	NEAR
		MOV	AX,BP
		CMP	AL,0
		JZ	L_26FA
		MOV	DL,1
		MOV	DH,D_4A
		DEC	DH
		CMP	AL,DH
		JZ	L_26FA
		INC	DL
L_26FA:
		XOR	DH,DH
		RET
S_26E6		ENDP

;子程序:
S_26FD		PROC	NEAR
		MOV	AX,BP
		TEST	DH,80H
		JNZ	L_2717
		CALL	S_275A
		MOV	DS:[DI+D_ZFQ],CL
		MOV	DS:[DI+D_SXQ],BL
		MOV	BYTE PTR DS:[DI+D_BZQ],80H
		XOR	AH,AH
		RET
L_2717:
		TEST	DH,1
		JNZ	L_273C
		MOV	D_71,AX
		CALL	S_275A
		MOV	DS:[DI+D_ZFQ],CL
		MOV	WORD PTR DS:[DI+D_BZQ],201H
		MOV	DS:[DI+D_SXQ],BL
		MOV	DH,CL
		MOV	DL,DS:[DI+D_ZFQ+1]
L_2736:
		CALL	S_276C
		MOV	AH,2
		RET
L_273C:
		DEC	AL
		MOV	D_71,AX
		CALL	S_275A
		MOV	DS:[DI+D_ZFQ+1],CL
		MOV	WORD PTR DS:[DI+D_BZQ],201H
		MOV	DS:[DI+D_SXQ+1],BL
		MOV	DL,CL
		MOV	DH,DS:[DI+D_ZFQ]
		JMP	SHORT L_2736
S_26FD		ENDP

;子程序:
S_275A		PROC	NEAR
		CALL	S_1CFA
		SAR	AX,1
		MOV	DI,AX
		RET
S_275A		ENDP

;子程序:
S_2762		PROC	NEAR
		CALL	S_275A
		MOV	AL,DS:[DI+D_BZQ]
		CMP	AL,80H
		RET
S_2762		ENDP

;子程序:
S_276C		PROC	NEAR
		MOV	D_HZ9,DH
		MOV	AH,D_0069
		INT	7FH
		RET
S_276C		ENDP

;****************************************************************************
BEGIN:
		MOV	AH,-1			;取安装模块?		2.10
		INT	10H
		CMP	AX,63H			;CAG11:'c'
		JNZ	L_S10
		MOV	AX,0E07H
		INT	10H
		MOV	AX,4C01H
		INT	21H
L_S10:
		MOV	AH,2FH			;取键盘模块中断
		INT	16H
		OR	BP,BP			;判有键盘模块?
		JNZ	L_S12
		MOV	BP,CS
L_S12:
		MOV	D_INT16,BP

		MOV	AX,3510H		;取原INT10
		INT	21H
		MOV	WORD PTR D_INT10,BX	;保存
		MOV	WORD PTR D_INT10+2,ES

		MOV	DX,OFFSET INT_10
		MOV	AX,2510H
		INT	21H

		MOV	AX,6
		INT	10H

		MOV	DX,OFFSET D_MSG
		MOV	AH,9
		INT	21H

		MOV	DX,OFFSET BEGIN
		INT	27H

D_MSG		DB	'  HHBIOS 2.13L 汉字系统',13,10
		DB	'研制人:吴晓军  1996年10月',13,10,'$'

SEG_A		ENDS
		END	START
